/*
 * Copyright (c) 2013 Houbrechts IT
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath 'com.eriwen:gradle-js-plugin:1.8.0'
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.4'
    }
}


apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'js'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'

mainClassName = 'com.github.houbie.lesscss.Lessc'
version = '0.5-less-1.4.1'
group = 'com.github.houbie'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.mozilla:rhino:1.7R4'
    compile 'org.slf4j:slf4j-api:1.7.5'
    optional 'commons-cli:commons-cli:1.2'
    optional 'ch.qos.logback:logback-classic:1.0.13'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

minifyJs {
    source = files 'src/main/resources/js/environment.js', 'src/main/resources/js/less-1.4.1.js', 'src/main/resources/js/cssmin.js', 'src/main/resources/js/compile.js'
    dest = file("${buildDir}/resources/main/js/all-min.js")
}

task compileJs(type: JavaExec, dependsOn: minifyJs) {
    classpath configurations.compile
    main = 'org.mozilla.javascript.tools.jsc.Main'
    args = ['-d', "${buildDir}/classes/main", '-o', 'LessImpl.class', '-package', 'com.github.houbie.lesscss.compiledjs', '-opt', '9', '-version', '170', "${buildDir}/resources/main/js/all-min.js"]

}
//add generated class to compile classpath
sourceSets.main.compileClasspath += files("${buildDir}/classes/main")
compileJava.dependsOn compileJs

jar {
    manifest {
        attributes 'Implementation-Title': 'Java LESS compiler', 'Implementation-Version': version
    }
    from 'LICENSE', 'NOTICE'
}

applicationDistribution.from(configurations.optional) {
    into 'lib'
}

startScripts {
    classpath += configurations.optional
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
    archives distZip
}

//********* artifact signing *********
if (hasProperty("release")) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
    def isDevBuild
    def isCiBuild
    def isReleaseBuild

    def sonatypeRepositoryUrl

    //set build variables based on build type (release, continuous integration, development)
    if (hasProperty("release")) {
        isReleaseBuild = true
        sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    } else if (hasProperty("ci")) {
        isCiBuild = true
        version += "-SNAPSHOT"
        sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
    } else {
        isDevBuild = true
    }

    repositories {
        if (isDevBuild) {
            mavenLocal()
        } else {
            mavenDeployer {
                if (isReleaseBuild) {
                    beforeDeployment { deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name project.name
                    packaging 'jar'
                    description 'Lesscss is a java wrapper for less.js'
                    url 'https://github.com/houbie/lesscss'

                    scm {
                        url 'scm:git@github.com:houbie/lesscss.git'
                        connection 'scm:git@github.com:houbie/lesscss.git'
                        developerConnection 'scm:git@github.com:houbie/lesscss.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'houbie'
                            name 'Ivo Houbrechts'
                        }
                    }
                }
            }
        }
    }
}
