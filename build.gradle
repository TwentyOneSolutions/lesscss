/*
 * Copyright (c) 2013 Houbrechts IT
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath 'com.eriwen:gradle-js-plugin:1.8.0'
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.4'
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}


apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'js'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'
apply plugin: 'release'

//version -> version.txt
group = com.github.houbie

sourceCompatibility = 1.7
targetCompatibility = 1.7
mainClassName = 'com.github.houbie.lesscss.Lessc'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.mozilla:rhino:1.7R4'
    compile 'org.slf4j:slf4j-api:1.7.5'
    optional 'commons-cli:commons-cli:1.2'
    optional 'ch.qos.logback:logback-classic:1.0.13'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

minifyJs {
    source = files 'src/main/resources/js/environment.js', 'src/main/resources/js/less-1.4.1.js', 'src/main/resources/js/cssmin.js', 'src/main/resources/js/compile.js'
    dest = file("${buildDir}/resources/main/js/all-min.js")
}

task compileJs(type: JavaExec, dependsOn: minifyJs) {
    classpath configurations.compile
    main = 'org.mozilla.javascript.tools.jsc.Main'
    args = ['-d', "${buildDir}/classes/main", '-o', 'LessImpl.class', '-package', 'com.github.houbie.lesscss.compiledjs', '-opt', '9', '-version', '170', "${buildDir}/resources/main/js/all-min.js"]

}
//add generated class to compile classpath
sourceSets.main.compileClasspath += files("${buildDir}/classes/main")
compileJava.dependsOn compileJs

jar {
    manifest {
        attributes 'Implementation-Title': 'Java LESS compiler', 'Implementation-Version': version
    }
    from 'LICENSE', 'NOTICE'
}

applicationDistribution.from(configurations.optional) {
    into 'lib'
}
applicationDistribution.into('') {
    from 'README.md'
}

startScripts {
    classpath += configurations.optional
    applicationName = 'lessc'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
    archives distZip
}

//********* artifact signing and publishing *********

ext.sonatypeDeploy = hasProperty('sonatype')
if (sonatypeDeploy) {
    signing {
        sign configurations.archives
    }
}
ext.isSnapshot = "$version".endsWith('-SNAPSHOT')
ext.sonatypeRepositoryUrl = isSnapshot ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'

uploadArchives {
    repositories {
        if (!sonatypeDeploy) {
            mavenLocal()
        } else {
            mavenDeployer {
                if (!isSnapshot) {
                    beforeDeployment { deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name project.name
                    packaging 'jar'
                    description 'Lesscss is a java wrapper for the JavaScript LESS to CSS compiler'
                    url 'https://github.com/houbie/lesscss'

                    scm {
                        url 'scm:git@github.com:houbie/lesscss.git'
                        connection 'scm:git@github.com:houbie/lesscss.git'
                        developerConnection 'scm:git@github.com:houbie/lesscss.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'houbie'
                            name 'Ivo Houbrechts'
                        }
                    }
                }
            }
        }
    }
}

//********* release plugin *********
task updateReadme << {
    File readme = file('README.md')
    //replace all occurrences of x.y.z-less-a.b.c with the new release version
    readme.text = (readme.text=~/\d\.\d(\.\d)?-less-\d\.\d(\.\d)?/).replaceAll("${ext['release.oldVersion']}")
}

commitNewVersion.dependsOn updateReadme
